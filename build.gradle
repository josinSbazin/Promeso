
buildscript {
    ext.kotlin_version = '1.1.51'
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.1'
    }
}

repositories {
    jcenter()
}

apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'org.junit.platform.gradle.plugin'

dependencies {
    testCompileOnly 'org.junit.jupiter:junit-jupiter-api:5.0.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.0.1'
    //testCompileOnly 'org.junit.platform:junit-platform-runner:1.0.1'
    api "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

group = 'com.github.romansl'
sourceCompatibility = 1.6
version = '1.1'

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
}

junitPlatform {
    details 'flat'
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

// To specify a license in the pom:
install {
    repositories.mavenInstaller {
        pom.project {
            licenses {
                license {
                    name 'MIT License'
                    url 'http://www.opensource.org/licenses/mit-license.php'
                    distribution 'repo'
                }
            }
        }
    }
}
